grammar org.metafacture.fix.Fix
  with org.eclipse.xtext.xbase.Xbase

generate fix "http://www.metafacture.org/fix/Fix"

Fix:
  elements += Expression*
;

Expression:
  Do |
  If |
  MethodCall |
  {Expression} SL_COMMENT
;

If:
  'if' name = ValidID '(' ( params += (QualifiedName|STRING) ( ',' params += (QualifiedName|STRING) )* )? ')' BEGIN
    elements += Expression*
  END
  elseIf = ElsIf?
  else = Else?
  'end'
;

ElsIf:
  'elsif' name = ValidID '(' ( params += (QualifiedName|STRING) ( ',' params += (QualifiedName|STRING) )* )? ')' BEGIN
    elements += Expression*
  END
;

Else:
  {Else} 'else' BEGIN
    elements += Expression*
  END
;

Do:
  'do' name = ValidID '(' ( params += (QualifiedName|STRING) ( ',' params += (QualifiedName|STRING) )* )? ')' BEGIN
    elements += Expression*
  END
  'end'
;

MethodCall:
  name = ValidID '(' ( params += (QualifiedName|STRING) ( ',' params += (QualifiedName|STRING) )* )? ')'
;

terminal fragment ESCAPED_CHAR:
    '\\' ('n'|'t'|'r'|'\\');
@Override
terminal STRING:
    '"' ( ESCAPED_CHAR | !('\\'|'"') )* '"' |
    "'" ( ESCAPED_CHAR | !('\\'|"'") )* "'";

terminal BEGIN:
  'synthetic:BEGIN'
;

terminal END:
  'synthetic:END'
;

@Override
terminal SL_COMMENT:
  '#' -> ( '\r'? '\n' )
;
